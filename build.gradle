plugins {
    id 'java'
    id "io.freefair.lombok" version "6.4.3"
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'idea'
}

repositories {
    mavenCentral()
}

group 'io.zyient.framework'
version '1.0-SNAPSHOT'

configurations.all {
    exclude module: "logback-classic"
    exclude module: "log4j-to-slf4j"
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.name == 'log4j') {
            details.useTarget "org.apache.logging.log4j:log4j-slf4j18-impl:2.22.0"
        }
        if (details.requested.name == 'avro') {
            details.useTarget "org.apache.avro:avro:1.11.0"
        }
        if (details.requested.name == 'commons-codec') {
            details.useTarget 'commons-codec:commons-codec:1.16.0'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "io.zyient.framework"
            from components.java

            pom {
                name = 'Zyient Java Core Framework'
                description = 'Zyient Java - Core Framework Libraries.'
                url = 'https://zyient.io/'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'subhagho'
                        name = 'Subho Ghosh'
                        email = 'subho.ghosh@outlook.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:subhagho/hcdc.git'
                    developerConnection = 'scm:git:git@github.com:subhagho/hcdc.git'
                    url = 'https://zyient.io'
                }
            }
        }
    }

}

apply plugin: 'maven-publish'
subprojects {

    apply plugin: 'java'
    apply plugin: "io.freefair.lombok"
    apply plugin: 'maven-publish'
    apply plugin: 'java-library'
    apply plugin: 'idea'

    group 'io.zyient.framework'
    version '1.0-SNAPSHOT'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
        withSourcesJar()
    }

    repositories {
        mavenCentral()
        maven {
            url layout.buildDirectory.dir(repoDirSnapshot)
            mavenContent {
                snapshotsOnly()
            }
        }
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
            mavenContent {
                snapshotsOnly()
            }
        }
    }

    publishing {
        publications {
            "$project.name"(MavenPublication) {
                groupId = project.group
                artifactId = project.name
                version = project.version
                from components.java
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = layout.buildDirectory.dir(repoDirRelease)
                def snapshotsRepoUrl = layout.buildDirectory.dir(repoDirSnapshot)
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }

    configurations.all {
        exclude module: "logback-classic"
        exclude module: "log4j-to-slf4j"
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if (details.requested.name == 'log4j') {
                details.useTarget "org.apache.logging.log4j:log4j-slf4j18-impl:2.22.0"
            }
            if (details.requested.name == 'avro') {
                details.useTarget "org.apache.avro:avro:1.11.0"
            }
            if (details.requested.name == 'curator-recipes') {
                details.useTarget 'org.apache.curator:curator-recipes:5.4.0'
            }
            if (details.requested.name == 'curator-framework') {
                details.useTarget 'org.apache.curator:curator-framework:5.4.0'
            }
            if (details.requested.name == 'curator-client') {
                details.useTarget 'org.apache.curator:curator-client:5.4.0'
            }
            if (details.requested.name == "commons-codec") {
                details.useTarget 'commons-codec:commons-codec:1.16.0'
            }
            /*
            if (details.requested.name == "antlr-runtime") {
                details.useTarget 'org.antlr:antlr4-runtime:4.7.1'
            }
             */
        }
    }

    dependencies {
        // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
        implementation 'org.slf4j:slf4j-api:2.0.9'
        implementation 'org.apache.logging.log4j:log4j-api:2.22.0'
        implementation 'org.apache.logging.log4j:log4j-core:2.22.0'
        // https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-slf4j18-impl
        implementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.22.0'
        implementation 'com.expediagroup.beans:bull-bean-transformer:2.2.3'

        implementation 'com.google.guava:guava:31.1-jre'
        implementation 'org.apache.commons:commons-lang3:3.12.0'
        implementation 'org.projectlombok:lombok:1.18.24'
        implementation 'commons-beanutils:commons-beanutils:1.9.4'
        implementation 'commons-io:commons-io:2.11.0'
        implementation 'commons-codec:commons-codec:1.16.0'

        implementation 'org.apache.commons:commons-configuration2:2.7'
        // https://mvnrepository.com/artifact/com.beust/jcommander
        implementation group: 'com.beust', name: 'jcommander', version: '1.82'

        implementation 'com.fasterxml.jackson.core:jackson-core:2.15.2'
        implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
        implementation 'com.fasterxml.jackson.core:jackson-annotations:2.15.2'
        implementation 'com.fasterxml.jackson.datatype:jackson-datatype-joda:2.15.2'
        implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.15.2'

        // https://mvnrepository.com/artifact/com.google.protobuf/protobuf-java
        implementation 'com.google.protobuf:protobuf-java:3.21.5'
        // https://mvnrepository.com/artifact/com.google.protobuf/protobuf-java-util
        implementation 'com.google.protobuf:protobuf-java-util:3.21.5'
        implementation 'org.apache.curator:curator-recipes:5.4.0'
        implementation 'org.apache.curator:curator-framework:5.4.0'
        implementation 'org.apache.curator:curator-client:5.4.0'

        implementation 'io.prometheus:simpleclient:0.16.0'
        implementation 'io.prometheus:simpleclient_log4j2:0.16.0'
        implementation 'io.prometheus:simpleclient_hotspot:0.16.0'
        implementation 'io.prometheus:simpleclient_httpserver:0.16.0'
        implementation 'io.prometheus:simpleclient_pushgateway:0.16.0'
        implementation 'io.micrometer:micrometer-registry-prometheus:1.10.5'

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    }

    test {
        useJUnitPlatform()
    }

}